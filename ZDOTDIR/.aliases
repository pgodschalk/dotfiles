# shellcheck shell=zsh

# Easier navigation: -
alias -- -="builtin cd -"

# Shortcuts
[[ -d "${XDG_DOWNLOAD_DIR}" ]] && alias dl="cd '${XDG_DOWNLOAD_DIR}'"
[[ -d "${XDG_DESKTOP_DIR}" ]] && alias dt="cd '${XDG_DESKTOP_DIR}'"
[[ -d "${HOME}/Developer" ]] && alias dv="cd '${HOME}/Developer'"

# Use eza as ls and tree
if (( $+commands[eza] )); then
  alias ls="eza --classify --git --icons"
  alias tree="eza --tree --git --icons"
  alias la="ll"
  alias l="ls -1"
fi

# Get week number
alias week='date +%V'

# Normalize `open` across Linux, macOS, and Windows.
if [[ "${DISTRO}" == "wsl" ]]; then
  alias open='explorer.exe'
elif [[ "${DISTRO}" != "macos" ]]; then
  alias open='xdg-open'
fi

if [[ "${DISTRO}" == "macos" ]]; then
  # Google Chrome with debugging port
  _chrome='/Applications/Google Chrome.app/Contents/MacOS/Google Chrome'
  [[ -x "${_chrome}" ]] \
    && alias chrome="'${_chrome}' --remote-debugging-port=9222"
  unset _chrome

  # IP address
  alias localip="ipconfig getifaddr en0"

  # Show active network interfaces
  (( $+commands[pcregrep] )) && alias ifactive="ifconfig | pcregrep \
    --multiline --only-matching '^[^\t:]+:([^\n]|\n\t)*status: active'"

  # Empty the Trash on all mounted volumes and the main HDD.
  # Also, clear Apple's System Logs to improve shell startup speed.
  # Finally, clear download history from quarantine. https://mths.be/bum
  # shellcheck disable=SC2154
  alias emptytrash="\
    sudo \\rm -rfv /Volumes/*/.Trashes(N); \
    sudo \\rm -rfv '${HOME}/.Trash'/*(N); \
    sudo \\rm -rfv /private/var/log/asl/*.asl(N); \
    sqlite3 \
    '${HOME}/Library/Preferences'/com.apple.LaunchServices.QuarantineEventsV*(N) \
    'delete from LSQuarantineEvent'"

  # Stuff I never really use but cannot delete either because of
  # http://xkcd.com/530/
  alias stfu="osascript -e 'set volume output muted true'"
  alias pumpitup="osascript -e 'set volume output volume 100'"
fi

# Get public IP addresses
(( $+commands[doggo] )) && \
  alias ip="doggo myip.opendns.com @resolver1.opendns.com --short -4 && \
    doggo myip.opendns.com @resolver1.opendns.com --short -6"

# Canonical hex dump; some systems have this symlinked
(( ! $+commands[hd] && $+commands[hexdump] )) && alias hd="hexdump -C"

# macOS has no `md5sum`, so use `md5` as a fallback
(( ! $+commands[md5sum] && $+commands[md5] )) && alias md5sum="md5"

# macOS has no `sha1sum`, so use `shasum` as a fallback
(( ! $+commands[sha1sum] && $+commands[shasum] )) && alias sha1sum="shasum"

# Trim new lines and copy to clipboard
if [[ "${DISTRO}" == "macos" ]]; then
  alias c="tr -d '\n' | pbcopy"
elif (( $+commands[xclip] )); then
  alias c="tr -d '\n' | xclip -selection clipboard"
elif (( $+commands[xsel] )); then
  alias c="tr -d '\n' | xsel --clipboard"
fi

# URL-encode strings
if (( $+commands[python3] )); then
  alias urlencode='python3 -c \
    "import sys,urllib.parse;print(urllib.parse.quote_plus(sys.argv[1]))"'
elif (( $+commands[python] )); then
  alias urlencode='python -c \
    "import sys,urllib.parse;print(urllib.parse.quote_plus(sys.argv[1]))"'
fi

# Intuitive map function
# For example, to list all directories that contain a certain file:
# find . -name .gitattributes | map dirname
alias map="xargs -n 1"

# Reload the shell (i.e. invoke as a login shell)
alias reload='exec ${SHELL} -l'

# Print each PATH entry on a separate line
alias path='print -l ${(s/:/)PATH}'

# Set up aliases for jj
if (( $+commands[jj] )); then
  alias jd="jj desc"
  alias jf="jj git fetch"
  alias jn="jj new"
  alias jp="jj git push"
  alias js="jj st"
fi

# Node aliases from
# <https://github.com/sorin-ionescu/prezto/tree/master/modules/node>
# Skips having to load the entire (slow!) node module
if (( $+commands[npm] )); then
  alias npmi="npm install"
  alias npml="npm list"
  alias npmo="npm outdated"
  alias npmp="npm publish"
  alias npmP="npm prune"
  alias npmr="npm run"
  alias npms="npm search"
  alias npmt="npm test"
  alias npmu="npm update"
  alias npmx="npm uninstall"
  alias npmci="npm ci"
  alias npmcit="npm cit"
  alias npmit="npm it"
fi

# Python aliases from
# <https://github.com/sorin-ionescu/prezto/tree/master/modules/python>
# Skips having to load the entire (slow!) python module
(( $+commands[python] )) && alias py="python"
(( $+commands[python3] )) && alias py3="python3"

# bat and bat-extras aliases
if (( $+commands[bat] || $+commands[batcat] )); then
  (( $+commands[batcat] )) && alias bat="batcat"
  alias cat="bat --paging=never"
  alias -g -- -h='-h 2>&1 | bat --language=help --style=plain'
  alias -g -- --help='--help 2>&1 | bat --language=help --style=plain'
  (( $+commands[batgrep] && $+commands[rg] )) && alias rg="batgrep"
  (( $+commands[batpipe] )) && alias lesspipe="batpipe"
  (( $+commands[batwatch] )) && alias watch="batwatch --command"
fi

# Use jaq as jq
(( $+commands[jaq] )) && alias jq="jaq"

# Use miniserve as http-serve
(( $+commands[miniserve] )) && alias http-serve="miniserve ."

# AI commit message to clipboard
(( $+commands[aicommit2] && $+commands[op] )) && alias aic='\
  OPENAI_API_KEY="op://Private/sgjddcuzvgu3qbpzqqw6tn5lw4/credential" \
  op run -- aicommit2 --include-body'

# Use 1Password for llm API keys
(( $+commands[llm] && $+commands[op] )) && alias llm='\
  OPENAI_API_KEY="op://Private/sgjddcuzvgu3qbpzqqw6tn5lw4/credential" \
  op run -- llm'

# Use 1Password for minisign private key
if (( $+commands[minisign] && $+commands[op] )); then
  minisign() {
    local keyfile
    keyfile=$(mktemp) || return 1
    {
      op read "op://Private/6w2qki2ijachamuvgbglgvxtda/private key" \
        | sed 's/\(secret key\) /\1\n/' >| "${keyfile}" && \
        command minisign -s "${keyfile}" "$@"
    } always {
      rm -f "${keyfile}"
    }
  }
fi

# Alias wget to use custom hsts cache file location
(( $+commands[wget] )) && \
  alias wget='wget --hsts-file="${XDG_DATA_HOME}"/wget-hsts'
